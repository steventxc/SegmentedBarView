version = "1.1.6"
group = "mobi.gspd"

def siteUrl = 'https://github.com/gspd-mobi/SegmentedBarView'
def gitUrl = 'https://github.com/gspd-mobi/SegmentedBarView.git'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    resourcePrefix "sbv_"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                artifactId = 'segmentedbarview'
                packaging 'aar'

                name 'Custom UI control for android which is showing data as a segments and a value inside them.'
                url siteUrl

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                    }
                }
                developers {
                    developer {
                        id 'PavelKorolev'
                        name 'Pavel Korolev'
                        email 'pavel.korolev@gspd.mobi'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        name = 'mobi.gspd.segmentedbarview'
        repo = "segmentedbarview"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        userOrg = 'gspd-mobi'
        licenses = ["MIT"]
        publish = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:27.0.2'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

